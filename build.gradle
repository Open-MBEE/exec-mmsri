buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'org.hidetake.swagger.generator' version '2.18.2'
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation (
            "org.openmbee.mms:authenticator:${mmsVersion}",
            "org.openmbee.mms:localuser:${mmsVersion}",
            "org.openmbee.mms:ldap:${mmsVersion}",
            "org.openmbee.mms:crud:${mmsVersion}",
            "org.openmbee.mms:cameo:${mmsVersion}",
            "org.openmbee.mms:elastic:${mmsVersion}",
            "org.openmbee.mms:jupyter:${mmsVersion}",
            "org.openmbee.mms:permissions:${mmsVersion}",
            "org.openmbee.mms:webhooks:${mmsVersion}",
            "org.openmbee.mms:search:${mmsVersion}",
            "org.openmbee.mms:storage:${mmsVersion}",
            "org.openmbee.mms:groups:${mmsVersion}",
            "org.springframework.boot:spring-boot-starter-web",
            'org.springframework.boot:spring-boot-starter-actuator',
            "org.postgresql:postgresql:42.2.5",
            "org.springdoc:springdoc-openapi-ui:1.3.1",
            "org.springdoc:springdoc-openapi-security:1.3.1",
            "org.zalando:logbook-spring-boot-starter:2.2.1",
            "org.springframework:spring-aspects:5.3.4"
    )
    testImplementation(
            'org.springframework.boot:spring-boot-starter-test'
    )
    swaggerCodegen 'org.openapitools:openapi-generator-cli:5.0.1'
}

swaggerSources {
    python {
        inputFile = file('openapi/api-docs.yaml')
        code {
            language = 'python'
            configFile = file('openapi/python-config.json')
        }
    }
    java {
        inputFile = file('openapi/api-docs.yaml')
        code {
            language = 'java'
            configFile = file('openapi/java-config.json')
        }
    }
    doc {
        inputFile = file('openapi/api-docs.yaml')
        code {
            language = 'html2'
            configFile = file('openapi/doc-config.json')
        }
    }
}

springBoot {
    buildInfo()
}

bootJar {
    layered()
}

//https://stackoverflow.com/questions/61197984/bootjar-mavenjar-artifact-wasnt-produced-by-this-build
configurations {
    [apiElements, runtimeElements].each {
        it.outgoing.artifacts.removeIf { it.buildDependencies.getDependencies(null).contains(jar) }
        it.outgoing.artifact(bootJar)
    }
}
